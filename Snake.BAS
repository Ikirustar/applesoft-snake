0  GOSUB 50000
10  HOME : HTAB 10: VTAB 13: PRINT "LOADING GAME DATA..."
20  REM    GAME VARIABLES
38  LET RN = 5: LET MX = 200: LET HD = 0: LET TL = 0: LET W = 39: LET H = 39: LET PC = 1: LET PK = 0:SX =  INT (W / 2):SY =  INT (H / 2): DIM X(MX): DIM Y(MX): DIM PS(W,H): DIM PP(W,H): DIM PX(10): DIM PY(10)
70 SC = 4:XX = 11: LET FC = 1: LET SW = 1: LET SH = 1: LET VS = 0: LET HS = 0: LET WX = W * (1 - HS) / 2: LET WY = H * (1 - VS) / 2:X(0) = SX - 2:Y(0) = 19:X(1) = SX - 1:Y(1) = 19:X(2) = SX:Y(2) = 19
105  FOR I = 0 TO 2:PS(X(I),Y(I)) = 1: NEXT I:HD = 2:TL = 0
120  GOSUB 30000: REM    LOAD ROOM LAYOUT/DRAW CANVAS/SPAWN OBJECTS
150  COLOR= FC: LET FX = SX + 6: LET FY = SY: PLOT FX,FY: REM    SPAWN FIRST FOOD
161  COLOR= XX: PLOT X(TL),Y(TL): COLOR= SC: PLOT X(HD - 1),Y(HD - 1): PLOT X(HD),Y(HD): REM    STAND BY LOOP: DRAW STAND BY SNAKE
165  REM  Wait for movement
170  LET KI =  PEEK (49152): IF KI = 149 OR KI = 138 OR KI = 139 GOTO 184
180  GOTO 170
184  REM    INCREASE SNAKE BY ONE
186  GOSUB 3000:HD = HD + 1: IF HD > MX THEN HD = 0
190 X(HD) = SX:Y(HD) = SY:PS(SX,SY) = 1: PLOT X(HD),Y(HD)
199  REM    GAME LOOP
200  GOSUB 2000
210  END 
1000  GR 
1005  REM    COLORS
1006 CC = 13: REM    CANVAS COLOR
1007  LET BC = 5: REM    BLOCK COLOR
1030  REM    DRAW BACKGROUND
1035  COLOR= CC
1040  FOR I = 0 TO W
1041  VLIN 0,H AT I
1043  NEXT I
1100  RETURN 
2000  REM    GAME LOOP
2005  COLOR= XX: PLOT X(TL),Y(TL):PS(X(TL),Y(TL)) = 0: LET KI =  PEEK (49152): GOSUB 3000: IF SX = FX AND SY = FY THEN  GOSUB 6000: GOTO 2000
2012 PK = PP(SX,SY): IF PK <  > 0 THEN  GOSUB 7000
2014 TL = TL + 1: IF TL > MX THEN TL = 0
2016 HD = HD + 1: IF HD > MX THEN HD = 0
2017 X(HD) = SX:Y(HD) = SY: IF PS(SX,SY) = 1 THEN  GOSUB 9000: REM  Update snake position
2040  COLOR= SC: PLOT X(HD),Y(HD): IF PS(SX,SY) = 2 GOTO 2200: REM  Draw snake/check if touched transition block
2055 PS(SX,SY) = 1: GOTO 2005
2200  REM    ROOM CHANGE
2210  REM  SNAKE TRANSITION POSITION VARIABLES (I don't know what to name them)
2212 SW = 1:SH = 1:VS = 0:HS = 0
2220  IF KI = 136 THEN RN = RN - 1:SW = 1:SH = 2:HS = 1
2221  IF KI = 149 THEN RN = RN + 1:SW = W:SH = 2:HS =  - 1
2222  IF KI = 138 THEN RN = RN + 3:SW = 2:SH = H:VS =  - 1
2223  IF KI = 139 THEN RN = RN - 3:SW = 2:SH = 1:VS = 1
2225  GOSUB 8000
2226  TEXT : HOME : PRINT "CLEARING ROOM DATA..."
2227  REM  CLEAR ROOM DATA
2228  FOR I = 0 TO W: FOR J = 0 TO H:PS(I,J) = 0: NEXT J: NEXT I
2230  GOSUB 30000
2235 SX = W / SW:SY = H / SH
2290  GOTO 2000
2300  RETURN 
3000  REM    CALCULATE MOVEMENT
3005  REM    ASSETS
3010  IF KI = 136 THEN SX = SX - 1
3020  IF KI = 149 THEN SX = SX + 1
3030  IF KI = 138 THEN SY = SY + 1
3040  IF KI = 139 THEN SY = SY - 1
3100  RETURN 
4000  TEXT : HOME 
4005  LET DI =  PEEK (49152)
4013  REM    CANVAS PROPERTIES
4015  VTAB 1: HTAB 1: PRINT "WIDTH: " +  STR$ (W) + " HEIGHT: " +  STR$ (H)
4030  REM    KEY INPUT 
4031  PRINT "KEY INPUT: " +  STR$ (DI)
4040  REM    SNAKE PROPERTIES
4041  VTAB 3: HTAB 1: PRINT "HEAD: " +  STR$ (HD): VTAB 4: HTAB 1: PRINT "TAIL: " +  STR$ (TL): VTAB 5: HTAB 1: PRINT "LENGTH: " +  STR$ (HD - TL + 1): PRINT "SX: " +  STR$ (SX): PRINT "SY: " +  STR$ (SY)
4070  VTAB 15: PRINT "FOOD X: " +  STR$ (FX): VTAB 15: HTAB 12: PRINT "FOOD Y: " +  STR$ (FY)
4171  IF DI = 196 OR DI = 177 THEN  POKE 49152,149: GOTO 4190
4180  GOTO 4005
4185  REM  (Accomdating for classic/modern mode
4190  IF UI = 1 THEN  HOME : GOSUB 30000: GOSUB 11000
4195  IF UI = 2 THEN  HOME : GOSUB 52000
4200  RETURN 
5000  REM    RANDOM FOOD GENERATOR (MODERN)
5010 FX =  INT ( RND (1) * (W + 1))
5015 FY =  INT ( RND (1) * (W + 1))
5020  COLOR= FC
5025  PLOT FX,FY
5100  RETURN 
6000  REM    EAT FOOD AND GROW
6001  GOSUB 5000
6002 HD = HD + 1: IF HD > MX THEN HD = 0
6003 X(HD) = SX:Y(HD) = SY:PS(SX,SY) = 1: COLOR= SC: PLOT X(HD),Y(HD)
6010  FOR I = 1 TO 3
6015  GOSUB 3000:HD = HD + 1: IF HD > MX THEN HD = 0
6025 X(HD) = SX:Y(HD) = SY:PS(SX,SY) = 1: PLOT X(HD),Y(HD)
6040  NEXT I
6100  RETURN 
7000  REM  TELEPORT SNAKE TO PORTAL
7005  COLOR= 14: PLOT PX(PK + 1),PY(PK + 1): PLOT PX(PK),PY(PK): PLOT PX(PK + 1),PY(PK + 1): PLOT PX(PK),PY(PK): PLOT PX(PK + 1),PY(PK + 1): PLOT PX(PK),PY(PK)
7010 SX = PX(PK + 1)
7013 SY = PY(PK + 1)
7015 SX = SX - 1
7020  COLOR= 2: PLOT PX(PK),PY(PK): PLOT PX(PK + 1),PY(PK + 1)
7025  RETURN 
8000  REM  SCREEN TRANSITION ANIMATION
8005 WX = W * (1 - HS) / 2:WY = H * (1 - VS) / 2
8010  IF HS <  > 0 GOTO 8020
8015  IF VS <  > 0 GOTO 8025
8020  COLOR= 13: VLIN 0,39 AT WX:WX = WX + HS: COLOR= 5: VLIN 0,39 AT WX: IF WX >  = W OR WX <  = 0 THEN  RETURN 
8023  GOTO 8020
8025  COLOR= 13: HLIN 0,39 AT WY:WY = WY + VS: COLOR= 5: HLIN 0,39 AT WY: IF WY >  = H OR WY <  = 0 THEN  RETURN 
8027  GOTO 8025
9000  REM    DEATH SCREEN
9010  HOME : HGR2 
9011  REM    DRAW GAME OVER TEXT
9012  HPLOT 0,0 TO 279,0
9013  HPLOT 0,159 TO 279,159
9020  PRINT "TRY AGAIN? (Y/N): ";
9025  GET R$
9030  IF R$ = "Y" THEN  CLEAR : GOTO 10
9035  IF R$ = "N" THEN  CLEAR : TEXT : HOME : END 
9040  GOTO 9025
9100  RETURN 
11000  REM    REDRAW OBJECTS
11010  REM    SNAKE
11015  COLOR= SC
11022  FOR I = TL TO HD
11025  PLOT X(I),Y(I)
11030  NEXT I
11040  REM    FOOD
11045  COLOR= FC
11050  PLOT FX,FY
11100  RETURN 
15000  REM    DRAW TRANSITION BLOCK
15010  ON OC GOTO 15050,15060
15050  HLIN OX,OX + OS AT OY: RETURN 
15060  VLIN OY,OY + OS AT OX: RETURN 
16000  REM    DRAW PORTAL BLOCK
16005  ON NL GOTO 16010,16020,16030,16040,16050,16060
16010  PLOT OX,OY: COLOR= 2: PLOT OX + 1 + OS,OY: PLOT OX,OY + 1 + OC: RETURN 
16020  PLOT OX,OY: COLOR= 2: PLOT OX + 1 + OS,OY + 1 + OS: PLOT OX - 1 - OC,OY + 1 + OC: RETURN 
16030  PLOT OX,OY: COLOR= 2: PLOT OX,OY + 1 + OS: PLOT OX - 1 - OC,OY: RETURN 
16040  PLOT OX,OY: COLOR= 2: PLOT OX - 1 - OS,OY + 1 + OS: PLOT OX - 1 - OC,OY - 1 - OC: RETURN 
16050  PLOT OX,OY: COLOR= 2: PLOT OX - 1 - OS,OY: PLOT OX,OY - 1 - OC: RETURN 
16060  PLOT OX,OY: COLOR= 2: PLOT OX - 1 - OS,OY - 1 - OS: PLOT OX + 1 + OC,OY - 1 - OC: RETURN 
30000  REM  LOAD ROOM
30003  RESTORE 
30010  GOSUB 34000
30012  HTAB 10: VTAB 15: PRINT "FINDING ROOM NUMBER..."
30015  GOSUB 35000
30020  HTAB 10: VTAB 17: PRINT "STORING OBJECTS..."
30025  READ NL
30030  IF NL = 0 THEN  GOTO 30090
30035  READ OI,OX,OY,OS,OC
30040  IF OC <  > 13 THEN  GOSUB 32000
30045  GOTO 30025
30090  RESTORE : GR : GOSUB 35000: GOTO 30110
30095  REM    BACKGROUND/BORDERS
30110  READ NL: IF NL = 0 THEN  PRINT "Test!!!!": RETURN 
30130  READ OI,OX,OY,OS,OC
30140  GOSUB 31000
30200  GOTO 30110
31000  REM    OBJECTS/ASSETS
31005  COLOR= OC
31010  ON OI GOTO 31020,31030,31040,31050
31020  FOR I = 0 TO NL - 1: HLIN OX,OX + OS AT OY + I: NEXT I: RETURN 
31030  FOR I = 0 TO NL - 1: VLIN OY,OY + OS AT OX + I: NEXT I: RETURN 
31035  REM    TRANSITION BLOCK (DRAW)
31040  COLOR= 9: GOSUB 15000: RETURN 
31045  REM  PORTAL BLOCK (DRAW)
31050  COLOR= 3: GOSUB 16000: RETURN 
32000  REM    OBJECT STORING
32010  ON OI GOTO 32020,32030,32040,32050
32020  FOR I = 0 TO NL - 1: FOR J = OX TO (OX + OS):PS(J,OY + I) = 1: NEXT J: NEXT I: RETURN 
32030  FOR I = 0 TO NL - 1: FOR J = OY TO (OY + OS):PS(OX + I,J) = 1: NEXT J: NEXT I: RETURN 
32035  REM    TRANSITION BLOCK (STORE)
32040  ON OC GOTO 32043,32045
32043  FOR I = OX TO (OX + OS):PS(I,OY) = 2: NEXT I: RETURN 
32045  FOR I = OY TO (OY + OS):PS(OX,I) = 2: NEXT I: RETURN 
32047  REM  PORTAL BLOCK (STORE)
32050  ON NL GOTO 32055,32060,32065,32070,32075,32080
32055 PX = OX + 1 + OS:PY = OY:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX:PY = OY + 1 + OC:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PC = PC + 1: RETURN 
32060 PX = OX + 1 + OS:PY = OY + 1 + OS:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX - 1 - OC:PY = OY + 1 + OC:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY: RETURN 
32065 PX = OX:PY = OY + 1 + OS:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX - 1 - OC:PY = OY:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PC = PC + 1: RETURN 
32070 PX = OX - 1 - OS:PY = OY + 1 + OS:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX - 1 - OC:PY = OY - 1 - OC:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY: RETURN 
32075 PX = OX - 1 - OS:PY = OY:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX:PY = OY - 1 - OC:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY: RETURN 
32080 PX = OX - 1 - OS:PY = OY - 1 - OS:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY:PX = OX + 1 + OC:PY = OY - 1 - OC:PC = PC + 1:PP(PX,PY) = PC:PX(PC) = PX:PY(PC) = PY: RETURN 
32100  RETURN 
33000  REM FORMERLY ROOM FOUR
33027  READ RN$
33030  READ NL
33035  IF NL = 0 THEN  GOTO 33090
33037  READ OI,OX,OY,OS,OC
33040  IF OC <  > 13 THEN  GOSUB 32000
33045  GOTO 33030
33090  RESTORE : GR : READ RN$: PRINT RN$
33091  ONERR  GOTO 33094
33092  IF RN$ = "ROOM FOUR" THEN  GOTO 33115
33093  READ NL: GOTO 33092
33094  READ RN$: PRINT RN$
33095  RESUME 
33100  REM     BACKGROUND/BORDERS
33110  READ NL: IF NL = 0 THEN  RETURN 
33115  PRINT NL: GOSUB 16000
33130  READ OI,OX,OY,OS,OC
33140  GOSUB 31000
33200  GOTO 33110
34000  REM  ROOM SELECTION
34010  ON RN GOTO 34015,34020,34025,34030,34035,34040,34045,34050,34055
34015 R$ = "ROOM ONE": RETURN 
34020 R$ = "ROOM TWO": RETURN 
34025 R$ = "ROOM THREE": RETURN 
34030 R$ = "ROOM FOUR": RETURN 
34035 R$ = "ROOM FIVE": RETURN 
34040 R$ = "ROOM SIX": RETURN 
34045 R$ = "ROOM SEVEN": RETURN 
34050 R$ = "ROOM EIGHT": RETURN 
34055 R$ = "ROOM NINE": RETURN 
35000  REM   FIND ROOM NUMBER (NO ONERR)
35010  READ RN$
35015  IF RN$ = R$ THEN  RETURN 
35020  REM   Skip this room's data until NL=0
35025  READ NL
35030  IF NL = 0 THEN  GOTO 35010: REM   End of room, try next
35035  READ OI,OX,OY,OS,OC
35040  GOTO 35025
35045  READ RN$: PRINT RN$: PRINT "ITERATION: " +  STR$ (IT): PRINT "NL: " +  STR$ (NL)
36000  REM  DRAW COLLISION
36010  COLOR= 15: FOR I = 0 TO W: FOR J = 0 TO H
36012  IF PS(I,J) = 1 THEN  PLOT I,J
36015  NEXT J: NEXT I
36020  RETURN 
40000  REM  ROOM LAYOUTS
40001  REM     DATA PARAMETERS (REPETITIONS, OBJECT ID, X, Y, SIZE, COLOR)
40002  DATA  "ROOM TWO"
40004  DATA     40, 2, 0,  0, 39, 13, 1, 1, 0, 0, 39, 5, 1, 2, 0, 0, 39, 5 , 1, 1, 0, 39, 39,  5, 1, 2, 39, 0 , 39, 5
40005  DATA  19, 1, 0, 21, 14, 5, 19, 1,21, 21, 17, 5, 15, 1, 0, 0, 39, 5
40006  DATA  1, 3,15,39, 5, 1, 1, 3, 0,15, 5, 2, 1, 3,39,15, 5, 2, 0
40010  DATA    "ROOM FOUR"
40015  DATA     40, 2, 0,  0, 39, 13, 1, 1, 0, 0, 39, 5, 1, 2, 0, 0, 39, 5 , 1, 1, 0, 39, 39,  5, 1, 2, 39, 0 , 39, 5
40017  DATA  2, 1, 0,  3, 16, 5, 1, 1, 14, 9, 19, 5, 1, 1, 25,14, 7 , 5, 2, 2, 7 ,20,12 , 5, 3, 1, 4 ,25, 7 , 5
40020  DATA    1, 3,15, 0, 5, 1, 1, 3,39,15, 5, 2, 1, 3,15,39, 5, 1, 1, 4, 6,24, 2, 3,3, 4,32, 9, 4,17, 0
40025  DATA    "ROOM FIVE"
40030  DATA    40, 2, 0,  0, 39, 13, 1, 1, 0, 0, 39, 5, 1, 2, 0, 0, 39, 5 , 1, 1, 0, 39, 39,  5, 1, 2, 39, 0 , 39, 5
40035  DATA    4, 1, 5, 8, 25, 5, 3, 1, 5, 15, 6, 5, 1, 1,25, 15, 6, 5, 1, 2,31, 15, 6, 5, 3, 1,22, 21, 6, 5
40040  DATA    1, 3,15, 0, 5, 1, 1, 3,15,39, 5, 1, 1, 3, 0,15, 5, 2, 1, 3,39,15, 5, 2, 0
40045  DATA    "ROOM SIX"
40050  DATA    40, 2, 0,  0, 39, 13, 1, 1, 0, 0, 39, 5, 1, 2, 0, 0, 39, 5 , 1, 1, 0, 39, 39,  5, 1, 2, 39, 0 , 39, 5
40055  DATA    1, 3,15, 0, 5, 1, 1, 3,15,39, 5, 1, 1, 3, 0,15, 5, 2, 0
40060  DATA  "ROOM EIGHT"
40065  DATA  40, 2, 0,  0, 39, 13, 1, 1, 0, 0, 39, 5, 1, 2, 0, 0, 39, 5 , 1, 1, 0, 39, 39,  5, 1, 2, 39, 0 , 39, 5
40070  DATA  1, 3,15, 0, 5, 1, 1, 3, 0,15, 5, 2, 1, 3,39,15, 5, 2, 0
49999  RETURN 
50000  REM    MENU SELECT
50010  HOME 
50020  PRINT "1. MODERN MODE"
50030  PRINT "2. CLASSIC MODE"
50040  PRINT "3. QUIT"
50050  VTAB 10: PRINT "SELECT AN OPTION: ";
50055  GET UI
50060  ON UI GOTO 10,50075,50070
50070  HOME : END 
50075  HOME 
50085  PRINT "1. Easy"
50095  PRINT "2. Medium"
50105  PRINT "3. Hard"
50110  VTAB 10: PRINT "Select a difficulty: ";
50115  GET UI
50120  ON UI GOTO 51000
51000  REM    CLASSIC MODE
51003  HOME : LET W = 40: LET H = 24: LET MX = 500: DIM X(MX): DIM Y(MX): DIM PS(W,H):SX = W / 2:SY = H / 2: LET HD = 0: LET TL = 0:X(TL) = SX:Y(TL) = SY:X(HD) = X(TL):Y(HD) = Y(TL):PS(SX,SY) = 1: GOSUB 52000: GOSUB 53000: GOSUB 54000
52000  REM     DRAW CANVAS 
52002  HTAB 14: PRINT "<< SNAKE >>": VTAB 3: PRINT " XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX ": LET VP = 3: FOR I = 3 TO 21: HTAB 2: VTAB (I): PRINT "X": NEXT I: VTAB 3: HTAB 1: FOR I = 3 TO 21: HTAB 39: PRINT "X": NEXT I
52010  VTAB 22: PRINT " XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX "
52120  RETURN 
53000  REM    RANDOM FOOD GENERATOR (CLASSIC)
53010  LET FX =  INT ( RND (1) * (W - 4)) + 3
53015  LET FY =  INT ( RND (1) * (H - 6)) + 4
53020  HTAB FX: VTAB FY: PRINT "*"
53030  RETURN 
54000  REM    GAME LOOP (CLASSIC)
54005  LET KI =  PEEK (49152): VTAB Y(TL): HTAB X(TL): PRINT " ":PS(X(TL),Y(TL)) = 0: GOSUB 3000:HD = HD + 1:TL = TL + 1
54035  IF HD > MX THEN HD = 0
54040  IF TL > MX THEN TL = 0
54041 X(HD) = SX:Y(HD) = SY: IF PS(SX,SY) = 1 THEN  GOSUB 56000
54044  IF SY = FY AND SX = FX THEN  GOSUB 55000: GOSUB 53000: GOTO 54000
54050  VTAB SY: HTAB SX: PRINT "X":PS(SX,SY) = 1: VTAB SY: HTAB SX: PRINT "X"
54060  REM     CHECK IF TOUCHED WALLS
54070  IF SX = 2 OR SX = 39 OR SY = 3 OR SY = 22 THEN  GOSUB 56000
54087  GOTO 54000
54090  RETURN 
55000  REM    SNAKE GROWTH LOGIC
55010 HD = HD + 1
55020 X(HD) = FX:Y(HD) = FY
55025  HTAB X(HD): VTAB Y(HD): PRINT "X"
55027 PS(X(HD),Y(HD)) = 1
55030  RETURN 
56000  REM  DEATH SCREEN
56010  HOME 
56025  CLEAR 
56030  PRINT "GAME OVER"
56040  FOR I = 0 TO 1000: NEXT I
56042  PRINT " "
56045  PRINT "TRY AGAIN? (Y/N): ";
56050  GET R$
56060  IF R$ = "Y" THEN  GOTO 51000
56063  IF R$ = "N" THEN  TEXT : HOME : END 
56065  GOTO 56050
56100  RETURN 
